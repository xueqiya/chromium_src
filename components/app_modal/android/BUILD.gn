# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//ui/android/features.gni")

android_library("java") {
  sources = [
    "java/src/org/chromium/components/app_modal/JavascriptAppModalDialog.java",
    "java/src/org/chromium/components/app_modal/JavascriptDialogCustomView.java",
    "java/src/org/chromium/components/app_modal/JavascriptModalDialog.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]

  deps = [
    ":java_resources",
    ":jni_headers",
    "//base:base_java",
    "//base:jni_java",
    "//components/browser_ui/styles/android:java",
    "//components/browser_ui/widget/android:java",
    "//ui/android:ui_java",
  ]
}

android_resources("java_resources") {
  custom_package = "org.chromium.components.app_modal"
  resource_dirs = [ "java/res" ]

  deps = [
    ":app_modal_strings_grd",
    "//components/browser_ui/strings/android:browser_ui_strings_grd",
    "//components/browser_ui/styles/android:java_resources",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/components/app_modal/JavascriptAppModalDialog.java",
  ]
}

java_strings_grd("app_modal_strings_grd") {
  grd_file = "app_modal_android_strings.grd"
  outputs = [ "values/app_modal_android_strings.xml" ] +
            process_file_template(
                android_bundle_locales_as_resources,
                [ "values-{{source_name_part}}/app_modal_android_strings.xml" ])
}
