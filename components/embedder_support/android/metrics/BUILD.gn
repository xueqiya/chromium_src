# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

assert(is_android, "This component can only be depended on for android builds")

# Intended for content embedders such as WebView & WebLayer.
static_library("metrics") {
  sources = [
    "android_metrics_log_uploader.cc",
    "android_metrics_log_uploader.h",
    "android_metrics_service_client.cc",
    "android_metrics_service_client.h",
    "memory_metrics_logger.cc",
    "memory_metrics_logger.h",
  ]

  deps = [
    ":jni",
    "//components/metrics",
    "//components/metrics:net",
    "//components/metrics:ui",
    "//components/prefs",
    "//content/public/browser",
    "//services/resource_coordinator/public/cpp/memory_instrumentation:browser",
  ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/components/metrics/AndroidMetricsLogUploader.java",
  ]

  deps = [
    "//base:base_java",
    "//base:jni_java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

generate_jni("jni") {
  sources = [
    "java/src/org/chromium/components/metrics/AndroidMetricsLogUploader.java",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "android_metrics_service_client_unittest.cc" ]
  deps = [
    ":metrics",
    "//base/test:test_support",
    "//components/metrics",
    "//components/prefs:test_support",
    "//testing/gtest",
  ]
}
